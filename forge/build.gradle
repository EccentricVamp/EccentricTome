plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'eccentric-tome.mod-conventions'
}

sourceSets.main.resources.srcDirs += 'src/generated/resources'

def baseProperties = is_testing ? [
    'mixin.env.remapRefMap'        : 'true',
    'mixin.env.refMapRemappingFile': file('build/createSrgToMcp/output.srg')
] : [];

def baseMods = {
    eccentrictome {
        source sourceSets.main
        source project(':common').sourceSets.main
    }
}

minecraft {
    mappings channel: 'official', version: minecraft_version
    runs {
        client {
            workingDirectory file('run')
            properties baseProperties
            mods baseMods
        }

        server {
            workingDirectory file('run')
            properties baseProperties
            mods baseMods
        }

        data {
            workingDirectory file('run')
            properties baseProperties
            mods baseMods
            args(
                '--mod', mod_id,
                '--all',
                '--output', file('src/generated/resources/'),
                '--existing', file('src/main/resources/')
            )
        }
    }
}

repositories {
    mavenCentral()
    if (is_testing) {    
        maven { url 'https://maven.blamejared.com' }
        maven { url 'https://dvs1.progwml6.com/files/maven' }
        maven {
            url 'https://cursemaven.com'
            content {
                includeGroup 'curse.maven'
            }
        }
        maven {
            url 'https://maven.pkg.github.com/ModdingForBlockheads/Balm'
            credentials {
                username System.getenv('GITHUB_ACTOR')
                password System.getenv('GITHUB_TOKEN')
            }
        }
        maven {
            url "https://maven.pkg.github.com/ModdingForBlockheads/CookingForBlockheads"
            credentials {
                username System.getenv('GITHUB_ACTOR')
                password System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly project(':common')

    if (is_testing) {
        runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}")

        runtimeOnly fg.deobf('curse.maven:cucumber-272335:3686400')
        runtimeOnly fg.deobf('curse.maven:mystical-agriculture-246640:3686412')

        runtimeOnly fg.deobf("net.blay09.mods:balm:${balm_version}")
        runtimeOnly fg.deobf("net.blay09.mods:cookingforblockheads-forge:${cfb_version}")

        runtimeOnly fg.deobf('curse.maven:placebo-283644:3725741')
        runtimeOnly fg.deobf('curse.maven:apotheosis-313970:3717791')
    }
}

tasks.withType(JavaCompile) {
    source project(':common').sourceSets.main.allSource
}

jar.finalizedBy 'reobfJar'

curseforge {
    apiKey = System.getenv().CURSEFORGE_KEY
    project {
        id = curseforge_id
        releaseType = 'beta'
        addGameVersion 'Forge'
        addGameVersion minecraft_version
        addGameVersion "Java ${java_version}"
        mainArtifact jar
    }
}
